// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        kotlin_version = '1.3.70'
    }
    ext.general = [
            "android": [
                    "sdk": [
                            "compile": 29 as Integer,
                            "min"    : 21 as Integer,
                            "target" : 29 as Integer
                    ]
            ],
            "version": [
                    "code": 1,
                    "name": "0.0.1"
            ]
    ]

    ext.versions = [
            "androidx"  : [
                    "appcompat"       : "1.1.0",
                    "constraintlayout": "1.1.3",
                    "core"            : "1.2.0",
                    "lifecycle"       : "2.2.0",
                    "navigation"      : "2.3.0-alpha04"
            ],
            "google"    : [
                    "android": [
                            "material": "1.1.0"
                    ]
            ],
            "junit4"    : "4.13",
            "junit5"    : "5.1.0",
            "kotlin"    : [
                    "base"      : "1.3.72",
                    "coroutines": "1.3.5"
            ],
            "plugins"   : [
                    "android"     : "3.6.2",
                    "dependencies": "0.28.0"
            ],
            "threetenbp": [
                    "base"   : "1.4.4",
                    "android": "1.2.4"
            ],
            "truth": "1.0.1"
    ]

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.plugins.android}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.base}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${versions.plugins.dependencies}"
    }
}

apply plugin: "com.github.ben-manes.versions"

allprojects {

    version = general.version.name

    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Empty task assemble to trigger root jobs during normal builds
 */
task assemble() {}

/**
 * Update Travis file
 *
 * This will ensure that Travis file has up to date configuration that will match
 * project settings
 */
import org.apache.tools.ant.filters.ReplaceTokens

task updateTravisYml(type: Copy) {
    from 'dev/travis/.travis.yml'
    into '.'
    filter(ReplaceTokens, tokens: [
            ANDROID_SDK_COMPILE: general.android.sdk.compile.toString()
    ])
}

tasks.assemble.dependsOn("updateTravisYml")
tasks.clean.dependsOn("updateTravisYml")